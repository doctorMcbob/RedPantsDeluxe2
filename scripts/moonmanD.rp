REDPANTSGUY|128,256,32,64|True|
START:0 moonmanstand -16,-32
IDLE:0 moonmanstand -16,-32
RUNNING:0 moonmanrun0 -16,-32
RUNNING:4 moonmanrun1 -16,-32
RUNNING:8 moonmanrun2 -16,-32
RUNNING:12 moonmanrun3 -16,-32
AIRIAL:0 moonmanjump0 -16,-32
AIRIAL:0 moonmanjump2 -16,-32
AIRIAL:0 moonmanjump1 -16,-32
SLIDE:0 moonmanslide1 -16,-32
SLIDE:0 moonmanslide0 -16,-32
LANDING:0 moonmanland -16,-32
JUMPSQUAT:0 moonmanjumpsquat0 -16,-32
JUMPSQUAT:3 moonmanjumpsquat1 -16,-32
FLIPSQUAT:0 moonmanjumpsquat0 -16,-32
FLIPSQUAT:3 moonmanjumpsquat1 -16,-32
DIVESTART:0 moonmandivestart -16,-32
DIVE:0 moonmandive -32,0
DIVELAND:0 moonmandiveland -32,-0
ROLLOUT:0 moonmanrollout -16,0
BONK:0 moonmanbonk -16,-32
BONKLAND:0 moonmanbonkland0 -48,-0
BONKLAND:12 moonmanbonkland1 -32,-0
WALLTOUCH:0 moonmanwalljump0 -16,-32
WALLJUMP:0 moonmanwalljump1 -16,-32
WALLJUMP:3 moonmanwalljump2 -16,-32
SLIDEFLIP:0 moonmansideflip0 -48,0
SLIDEFLIP:8 moonmansideflip1 -48,0
SLIDEFLIP:10 moonmansideflip2 -16,0
SLIDEFLIP:14 moonmansideflip3 -16,0
SLIDEFLIP:18 moonmansideflip4 -16,0
SLIDEFLIP:20 moonmansideflip5 -16,-32
SLIDEFLIP:23 moonmansideflip6 -16,-32
CROUCH:0 moonmancrouch -16,0
LONGJUMPSTART:0 moonmanlongjump0 -16,-32
LONGJUMP:0 moonmanlongjump1 -16,-32
LONGJUMP:8 moonmanlongjump2 -16,-32
DEAD:0 moonmandead -16,-32
HITSTUN:0 moonmanbonk -16,-32
PUNCH:0 moonmanpunch0 -32,-32
PUNCH:4 moonmanpunch1 -16,-32
PUNCH:6 moonmanpunch2 -32,-32
PUNCH:8 moonmanpunch3 -16,-32
DANGLE:0 moonmandangle -32,-32
INVISIBLE:0 empty 0,0
DROWN:0 empty 0,0

|DROWN:45|
set self state IDLE
set self frame 0
exec respawn


|DANGLE:0|
if self.dangle_from
   set self x_vel 0
   set self y_vel 0
   set self y self.dangle_from.y + self.dangle_from.h - 32
   set self x self.dangle_from.x

   if A_DOWN in inpEVENTS
      set self state AIRIAL
      set self frame 0
      set self y_vel self.jumpstrength
   endif
endif


|START:0|
set self state IDLE
set self frame 0
set self speed 8
set self jumpstrength -17
set self hopstrength -12
set self divestrength 14
set self rolloutstrength -8
set self bonkstrength 2
set self walljumpstrength -12
set self walljumpoff 10
set self airdrift 0.7
set self traction 0.6 * -1
set self limit 20
set self airspeedcontrol 12
set self negairspeedcontrol self.airspeedcontrol * -1
set self negspeed self.speed * -1
set self neglimit self.limit * -1
if not self.grav
   set self grav 1
endif
set self slideflipxmod 3
set self slideflipstrength -24
set self longjumpystrength -9
set self longjumpxstrength 12

set self rotation 0
set self can_die 1

set self width 32
set self height 64

set self bloodmax 10
set self thwackmax 10
   
if COLLIDE?
   set self revert 1
endif

if not self.revert
   set self gravdir D
endif
if self.revert
   set self revert 0
   rebrand moonman + self.gravdir
endif

set self dustnumber 0
set self dustmax 10
set self dustfreq 4
set self dustcounter 0

|makeDust|
set self dustcounter self.dustcounter + 1
if self.dustcounter > self.dustfreq
   set self dustcounter 0
   set self dustname  dust + self.dustnumber
   set self dustnumber self.dustnumber + 1
   if self.dustnumber > self.dustmax
      set self dustnumber 0
   endif
   create dustbunny0 self.dustname dustbunny0.w / 2 * -1 + self.x + 16  self.y + self.h - dustbunny0.h
   set self.dustname x_vel self.x_vel * 1.1 
   set self.dustname y_vel -11 + RAND? + RAND? + RAND?
endif

|DEAD:0|
set self x_vel 0
set self y_vel 0
set self can_die 0
if self.frame % 4 == 0
   create blood0 self.name + blood + self.bloodnumber self.x + 16 self.y + 16
   set self bloodnumber self.bloodnumber + 1
   if self.bloodnumber > self.bloodmax
      set self bloodnumber 0
   endif
endif

|DEAD:45|
set self state IDLE
set self frame 0
exec respawn

|respawn|
set self x self.respawnx
set self y self.respawny
view self.frame_name self.respawnworld
move self.name self.respawnworld
if self.lakitu
   set self.lakitu x self.x
   set self.lakitu y self.y
   move self.lakitu self.respawnworld
endif
set self can_die 1

|IDLE:0|
set self door_key UP
exec checkAir
if self.x_vel != 0
   set self state SLIDE
endif
if self.y_vel == 0
   if not inpRIGHT and inpLEFT
      set self state RUNNING
      set self frame 0
      set self direction -1
   endif
   if not inpLEFT and inpRIGHT
      set self state RUNNING
      set self frame 0
      set self direction 1
   endif
endif
exec checkCrouch
exec checkJump
exec checkPunch
exec applyGrav

|IDLE:32|
exec IDLE:0
set self frame 0

|CROUCH:0|
exec checkAir
exec applyTraction
exec applyGrav
if not inpDOWN
   if self.x_vel == 0
      set self state IDLE
      set self frame 0
   endif
   if self.x_vel != 0
      set self state SLIDE
      set self frame 0
   endif
endif
# bufferable pog?
if self.x_vel != 0 and inpA
   set self state LONGJUMPSTART
   set self frame 0
endif
set self frame 0

|LONGJUMPSTART:0|
exec applyTraction
exec applyGrav

|LONGJUMPSTART:2|
exec LONGJUMPSTART:0
set self y_vel self.longjumpystrength
set self x_vel self.longjumpxstrength * self.direction
set self state LONGJUMP
set self frame 0

|LONGJUMP:0|
exec applyGrav
set self calc1 self.x_vel < self.negairspeedcontrol
if not self.calc1 and inpLEFT
   set self x_vel self.x_vel - self.airdrift
endif
set self calc1 self.x_vel > self.airspeedcontrol
if not self.calc1 and inpRIGHT
   set self x_vel self.x_vel + self.airdrift
endif

|RUNNING:0|
set self x_vel self.speed * self.direction
set self calc1 self.direction == 1 nor inpRIGHT
set self calc2 self.direction == -1 nor inpLEFT
if inpRIGHT == inpLEFT or self.calc1 or self.calc2
   set self state SLIDE
   set self frame 0
endif
exec checkCrouch
exec checkJump
exec checkPunch
exec checkAir
exec applyGrav

|RUNNING:16|
exec RUNNING:0
set self frame 0

|SLIDE:0|
if self.x_vel == 0
   set self state IDLE
   set self frame 0
endif
exec checkPunch
if self.x_vel != 0
   exec applyTraction
   set self absxvel abs self.x_vel
   if abs self.traction * 10 > self.absxvel
      img moonmanslide1
   endif
   exec checkCrouch
   exec checkJump
   if self.state == JUMPSQUAT
      if self.direction == 1 and inpLEFT
      	 set self state FLIPSQUAT
	 set self frame 0
      endif
      if self.direction == -1 and inpRIGHT
      	 set self state FLIPSQUAT
	 set self frame 0
      endif
   endif
   exec checkAir
   exec applyGrav
endif
if abs self.x_vel < self.speed

   set self calc1 self.x_vel > 0 and inpRIGHT
   set self calc1 not inpLEFT and self.calc1

   if self.calc1
      set self direction 1
   endif
   
   set self calc2 self.x_vel < 0 and inpLEFT
   set self calc2 not inpRIGHT and self.calc2

   if self.calc2
      set self direction -1
   endif
   
   if self.calc1 or self.calc2
      set self state RUNNING
      set self frame 0
   endif
endif

|AIRIAL:0|
set self calc1 self.y_vel > self.airspeedcontrol
set self calc2 self.airspeedcontrol * -1 > self.y_vel
if not self.calc1 nor self.calc2
   if self.x_vel - self.airdrift > self.negspeed and inpLEFT
	set self x_vel self.x_vel - self.airdrift
   endif
   if self.x_vel + self.airdrift < self.speed and inpRIGHT
      set self x_vel self.x_vel + self.airdrift
   endif
   if B_DOWN in inpEVENTS
      set self state DIVESTART
      set self frame 0
   endif
endif
if LEFT_DOWN in inpEVENTS
   rebrand moonmanL
endif
if UP_DOWN in inpEVENTS
   rebrand moonmanU
endif
if RIGHT_DOWN in inpEVENTS
   rebrand moonmanR
endif
if self.calc1
   img moonmanjump2
endif
if self.calc2
   img moonmanjump0
endif
exec applyGrav

|JUMPSQUAT:0|
exec applyTraction
exec applyGrav

|JUMPSQUAT:6|
set self state AIRIAL
if inpA
   set self y_vel self.jumpstrength
endif
if not inpA
   set self y_vel self.hopstrength
endif
set self frame 0

|LANDING:0|
exec checkAir
exec applyTraction
exec applyGrav
if self.x_vel > 0
   set self direction 1
endif
if self.x_vel < 0
   set self direction -1
endif

|LANDING:5|
if self.x_vel
   set self state SLIDE
   exec SLIDE:0
endif
if self.x_vel == 0
   set self state IDLE
   exec IDLE:0
endif

|DIVESTART:0|
set self y_vel 0
set self x_vel 0

|DIVESTART:5|
set self x_vel self.divestrength * self.direction
set self state DIVE

|DIVE:0|
exec applyGrav

|DIVELAND:0|
exec checkAir
exec applyTraction
exec applyGrav
# bufferable Pog?
if inpA
   set self state ROLLOUT
   set self frame 0
   set self y_vel self.rolloutstrength
endif
if self.x_vel == 0
   set self state IDLE
   set self frame 0
endif

|ROLLOUT:0|
exec applyGrav

|BONK:0|
exec applyGrav

|BONKLAND:0|
exec checkAir
exec applyTraction
exec applyGrav

|BONKLAND:25|
set self state IDLE
set self frame 0

|WALLTOUCH:0|
exec AIRIAL:0
set self state AIRIAL
if not self.calc1 nor self.calc2
   img moonmanwalljump0
   # bufferable pog?
   if inpA
      set self state WALLJUMP
      set self frame 0
   endif
endif

|WALLJUMP:0|
set self y_vel 0

|WALLJUMP:6|
set self state AIRIAL
set self frame 0
set self y_vel self.walljumpstrength
set self direction self.direction * -1
set self x_vel self.walljumpoff * self.direction

|FLIPSQUAT:0|
exec applyTraction
exec applyGrav

|FLIPSQUAT:6|
set self state SLIDEFLIP
set self direction self.direction * -1
set self x_vel self.slideflipxmod * self.direction
set self y_vel self.slideflipstrength
set self frame 0

|SLIDEFLIP:0|
exec applyGrav
if self.direction == -1
   if inpLEFT
      	set self x_vel self.x_vel - self.airdrift
	if self.x_vel < self.negairspeedcontrol
      	   set self x_vel self.negairspeedcontrol
   	endif
   endif
endif
if self.direction == 1
   if inpRIGHT
      set self x_vel self.x_vel + self.airdrift
      if self.x_vel > self.airspeedcontrol
      	 set self x_vel self.airspeedcontrol
      endif
   endif
endif

|PUNCH:0|
exec checkAir
exec applyTraction
exec applyGrav

|PUNCH:16|
set self state IDLE
set self frame 0

|HIT|
set self state HITSTUN
set self frame 0
set self direction related.direction * -1
if related.state == DIVE
   set self hit_x_vel 15 * related.direction
   set self hit_y_vel -10
endif
if related.state == PUNCH
   set self hit_x_vel 8 * related.direction
   set self hit_y_vel -8
endif

if self.thwacks == None
   set self thwacks 1
endif

if related.x <= self.x
   set self thw_x related.x + related.w
endif
if related.x > self.x
   set self thw_x related.x
endif
if related.y <= self.y
   set self thw_y self.y + related.y - self.y
endif
if related.y > self.y
   set self thw_y self.y + self.y - related.y
endif

create thwack0 self.name + thwack + self.thwacks self.thw_x self.thw_y
set related thwacks related.thwacks + 1
if related.thwacks > related.thwackmax
   set related thwacks 0
endif

|HITSTUN:0|
set self x_vel 0
set self y_vel 0

|HITSTUN:4|
set self state BONK
set self frame 0
set self x_vel self.hit_x_vel
set self y_vel self.hit_y_vel
set self hit_x_vel 0
set self hit_y_vel 0

|XCOLLISION|
if abs self.x_vel < 1
   set self x_vel 0
endif
set self calc1 self.state == DIVE
set self calc1 self.state == DIVELAND or self.calc1
set self calc1 self.state == LONGJUMP or self.calc1
if self.state == ROLLOUT or self.calc1
   set self state BONK
   set self x_vel self.bonkstrength * self.direction * -1
   set self frame 0
endif
set self calc1 self.state == SLIDEFLIP
if self.state == AIRIAL or self.calc1
   set self state WALLTOUCH
   set self frame 0
   if self.x_vel > 0
      set self direction 1
   endif
   if self.x_vel < 0
      set self direction -1
   endif
endif

|YCOLLISION|
if abs self.y_vel < 1
   set self y_vel 0
endif
set self calc1 self.state == ROLLOUT
set self calc1 self.state == SLIDEFLIP or self.calc1
set self calc1 self.state == LONGJUMP or self.calc1
if self.state == AIRIAL or self.calc1
   set self state LANDING
   set self frame 0
endif
if self.state == DIVE
   set self state DIVELAND
   set self frame 0
endif
if self.state == BONK
   set self state BONKLAND
   set self frame 0
endif

|COLLIDE|
if self.state == PLATFORM
   set self touched 1
endif
set self calc self.state == BANANA
set self calc self.state == CHEESE or self.calc
set self calc self.state == COLLECTABLE or self.calc
if self.state == COIN or self.calc
   set self collected 1
endif

|checkAir|
set self dangle_from 0
if self.y_vel
   set self state AIRIAL
   set self frame 0
endif

|applyGrav|
set self y_vel self.y_vel + self.grav
if self.y_vel > self.limit
   set self y_vel self.limit
endif
if self.y_vel < self.neglimit
   set self y_vel self.neglimit
endif

if abs self.y_vel < 1
   if self.y_vel > 0
      if self.grav > 0
         set self y_vel 1
      endif
   endif
   if self.y_vel < 0
      if self.grav < 0
         set self y_vel -1
      endif
   endif
endif

|applyTraction|
if self.x_vel != 0
   if self.x_vel > 0
      set self x_vel self.traction + self.x_vel
   endif
   if self.x_vel < 0
      set self x_vel self.traction * -1 + self.x_vel
   endif
   exec makeDust
endif
if abs self.x_vel / 1 == 0
   set self x_vel 0
endif

|checkCrouch|
if inpDOWN
   set self state CROUCH
   set self frame 0
endif

|checkJump|
if A_DOWN in inpEVENTS
   set self state JUMPSQUAT
   set self frame 0
endif

|checkPunch|
if B_DOWN in inpEVENTS
   set self state PUNCH
   set self frame 0
endif

