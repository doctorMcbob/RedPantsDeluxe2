REDPANTSGUY|128,256,32,64|True|
START:0 redpantsstand 0,-16
IDLE:0 redpantsstand 0,-16
RUNNING:0 redpantsrun0 0,-16
RUNNING:4 redpantsrun1 0,-16
RUNNING:8 redpantsrun2 0,-16
RUNNING:12 redpantsrun3 0,-16
AIRIAL:0 redpantsjump0 0,-16
AIRIAL:0 redpantsjump2 0,-16
AIRIAL:0 redpantsjump1 0,-16
SLIDE:0 redpantsslide1 0,-16
SLIDE:0 redpantsslide0 0,-16
LANDING:0 redpantsland 0,-16
JUMPSQUAT:0 redpantsjumpsquat0 0,-16
JUMPSQUAT:3 redpantsjumpsquat1 0,-16
FLIPSQUAT:0 redpantsjumpsquat0 0,-16
FLIPSQUAT:3 redpantsjumpsquat1 0,-16
DIVESTART:0 redpantsdivestart  0,-16
DIVE:0 redpantsdive 0,-16
DIVELAND:0 redpantsdiveland 0,-16
ROLLOUT:0 redpantsrollout 0,-16
BONK:0 redpantsbonk 0,-16
BONKLAND:0 redpantsbonkland0 0,-16
BONKLAND:12 redpantsbonkland1 0,-16
WALLTOUCH:0 redpantswalljump0 0,-16
WALLJUMP:0 redpantswalljump1 0,-16
WALLJUMP:3 redpantswalljump2 0,-16
SLIDEFLIP:0 redpantsdiveflip0 0,-16
SLIDEFLIP:8 redpantsdiveflip1 0,-16
SLIDEFLIP:10 redpantsdiveflip2 0,-16
SLIDEFLIP:14 redpantsdiveflip3 0,-16
SLIDEFLIP:18 redpantsdiveflip4 0,-16
SLIDEFLIP:20 redpantsdiveflip5 0,-16
SLIDEFLIP:23 redpantsdiveflip6 0,-16
CROUCH:0 redpantscrouch 0,-16
LONGJUMPSTART:0 redpantslongjumpstart 0,-16
LONGJUMP:0 redpantslongjump1 0,-16
LONGJUMP:8 redpantslongjump2 0,-16
DEAD:0 redpantsdead 0,-16

|START:0|
set self state IDLE
set self frame 0
set self speed 8
set self jumpstrength 17
set self hopstrength 12
set self divestrength 14
set self rolloutstrength 8
set self bonkstrength 2
set self walljumpstrength 12
set self walljumpoff 10
set self airdrift 0.7
set self traction 0.6 * -1
set self limit 20
set self airspeedcontrol 12
set self negairspeedcontrol self.airspeedcontrol * -1
set self negspeed self.speed * -1
set self neglimit self.limit * -1
set self grav 1
set self slideflipxmod 3
set self slideflipstrength 24
set self longjumpystrength 9
set self longjumpxstrength 12

set self rotation 270
set self can_die 1

set self width 64
set self height 32

if COLLIDE?
   set self revert moonman + self.gravdir
endif

if not self.revert
   set self gravdir L
endif
if self.revert
   set self revert 0
   rebrand moonman + self.gravdir
endif

|DEAD:0|
set self x_vel 0
set self y_vel 0
set self can_die 0

|DEAD:45|
set self state IDLE
self self frame 0
set self x self.respawnx
set self y self.respawny
set self can_die 1

|IDLE:0|
set self door_key RIGHT
exec checkAir
if self.y_vel != 0
   set self state SLIDE
endif
if self.x_vel == 0
   if not inpDOWN and inpUP
      set self state RUNNING
      set self frame 0
      set self direction -1
   endif
   if not inpUP and inpDOWN
      set self state RUNNING
      set self frame 0
      set self direction 1
   endif
endif
exec checkCrouch
     exec checkJump
exec applyGrav

|IDLE:32|
exec IDLE:0
set self frame 0

|CROUCH:0|
exec checkAir
exec applyTraction
exec applyGrav
if not inpLEFT
   if self.y_vel == 0
      set self state IDLE
      set self frame 0
   endif
   if self.y_vel != 0
      set self state SLIDE
      set self frame 0
   endif
endif
# bufferable pog?
if self.y_vel != 0 and inpA
   set self state LONGJUMPSTART
   set self frame 0
endif
set self frame 0

|LONGJUMPSTART:0|
exec checkAir
exec applyTraction
exec applyGrav

|LONGJUMPSTART:2|
exec LONGJUMPSTART:0
set self x_vel self.longjumpystrength
set self y_vel self.longjumpxstrength * self.direction
set self state LONGJUMP
set self frame 0

|LONGJUMP:0|
exec applyGrav
set self calc1 self.y_vel < self.negairspeedcontrol
if not self.calc1 and inpUP
   set self y_vel self.y_vel - self.airdrift
endif
set self calc1 self.y_vel > self.airspeedcontrol
if not self.calc1 and inpDOWN
   set self y_vel self.y_vel + self.airdrift
endif

|RUNNING:0|
set self y_vel self.speed * self.direction
set self calc1 self.direction == 1 nor inpDOWN
set self calc2 self.direction == -1 nor inpUP
if inpDOWN == inpUP or self.calc1 or self.calc2
   set self state SLIDE
   set self frame 0
endif
exec checkCrouch
exec checkJump
exec checkAir
exec applyGrav

|RUNNING:16|
exec RUNNING:0
set self frame 0

|SLIDE:0|
if self.y_vel == 0
   set self state IDLE
   set self frame 0
endif
if self.y_vel != 0
   exec applyTraction
   set self absxvel abs self.y_vel
   if abs self.traction * 10 > self.absxvel
      img redpantsslide1
   endif
   exec checkCrouch
   exec checkJump
   if self.state == JUMPSQUAT
      if self.direction == 1 and inpUP
      	 set self state FLIPSQUAT
	 set self frame 0
      endif
      if self.direction == -1 and inpDOWN
      	 set self state FLIPSQUAT
	 set self frame 0
      endif
   endif
   exec checkAir
   exec applyGrav
endif
if abs self.y_vel < self.speed
   set self calc1 self.y_vel > 0 and inpDOWN
   set self calc1 not inpUP and self.calc1
   if self.calc1
      set self direction 1
   endif
   if not inpDOWN and self.y_vel < 0 and inpUP
      set self direction -1
   endif
   if not inpDOWN and self.y_vel < 0 and inpUP or self.calc1
      set self state RUNNING
      set self frame 0
   endif
endif

|AIRIAL:0|
set self calc1 self.x_vel > self.airspeedcontrol
set self calc2 self.airspeedcontrol * -1 > self.x_vel
if not self.calc1 nor self.calc2
   if UP_DOWN in inpEVENTS
      rebrand moonmanU
   endif
   if RIGHT_DOWN in inpEVENTS
      rebrand moonmanR
   endif
   if DOWN_DOWN in inpEVENTS
      rebrand moonmanD
   endif
   if B_DOWN in inpEVENTS
      set self state DIVESTART
      set self frame 0
   endif
endif
if self.calc1
   img redpantsjump2
endif
if self.calc2
   img redpantsjump0
endif
exec applyGrav

|JUMPSQUAT:0|
exec checkAir
exec applyTraction
exec applyGrav

|JUMPSQUAT:6|
set self state AIRIAL
if inpA
   set self x_vel self.jumpstrength
endif
if not inpA
   set self x_vel self.hopstrength
endif
set self frame 0

|LANDING:0|
exec applyTraction
exec applyGrav
if self.y_vel > 0
   set self direction 1
endif
if self.y_vel < 0
   set self direction -1
endif

|LANDING:5|
if self.y_vel
   set self state SLIDE
   exec SLIDE:0
endif
if self.y_vel == 0
   set self state IDLE
   exec IDLE:0
endif

|DIVESTART:0|
set self y_vel 0
set self x_vel 0

|DIVESTART:5|
set self y_vel self.divestrength * self.direction
set self state DIVE

|DIVE:0|
exec applyGrav

|DIVELAND:0|
exec checkAir
exec applyTraction
exec applyGrav
# bufferable Pog?
if inpA
   set self state ROLLOUT
   set self frame 0
   set self x_vel self.rolloutstrength
endif
if self.y_vel == 0
   set self state IDLE
   set self frame 0
endif

|ROLLOUT:0|
exec applyGrav

|BONK:0|
set self y_vel self.bonkstrength * self.direction * -1
exec applyGrav

|BONKLAND:0|
exec checkAir
exec applyTraction
exec applyGrav

|BONKLAND:25|
set self state IDLE
set self frame 0

|WALLTOUCH:0|
exec AIRIAL:0
set self state AIRIAL
if not self.calc1 nor self.calc2
   img redpantswalljump0
   # bufferable pog?
   if inpA
      set self state WALLJUMP
      set self frame 0
   endif
endif

|WALLJUMP:0|
set self x_vel 0

|WALLJUMP:6|
set self state AIRIAL
set self frame 0
set self x_vel self.walljumpstrength
set self direction self.direction * -1
set self y_vel self.walljumpoff * self.direction

|FLIPSQUAT:0|
exec checkAir
exec applyTraction
exec applyGrav

|FLIPSQUAT:6|
set self state SLIDEFLIP
set self direction self.direction * -1
set self y_vel self.slideflipxmod * self.direction
set self x_vel self.slideflipstrength
set self frame 0

|SLIDEFLIP:0|
exec applyGrav
if self.direction == -1
   if inpUP
      	set self y_vel self.y_vel - self.airdrift
	if self.y_vel < self.negairspeedcontrol
      	   set self y_vel self.negairspeedcontrol
   	endif
   endif
endif
if self.direction == 1
   if inpDOWN
      set self y_vel self.y_vel + self.airdrift
      if self.y_vel > self.airspeedcontrol
      	 set self y_vel self.airspeedcontrol
      endif
   endif
endif

|YCOLLISION|
if abs self.y_vel < 1
   set self y_vel 0
endif
set self calc1 self.state == DIVE
set self calc1 self.state == DIVELAND or self.calc1
set self calc1 self.state == LONGJUMP or self.calc1
if self.state == ROLLOUT or self.calc1
   set self state BONK
   set self frame 0
endif
set self calc1 self.state == SLIDEFLIP
if self.state == AIRIAL or self.calc1
   set self state WALLTOUCH
   set self frame 0
   if self.y_vel > 0
      set self direction 1
   endif
   if self.y_vel < 0
      set self direction -1
   endif
endif

|XCOLLISION|
if abs self.x_vel < 1
   set self x_vel 0
endif
set self calc1 self.state == ROLLOUT
set self calc1 self.state == SLIDEFLIP or self.calc1
set self calc1 self.state == LONGJUMP or self.calc1
if self.state == AIRIAL or self.calc1
   set self x_vel 0
   set self state LANDING
   set self frame 0
endif
if self.state == DIVE
   set self state DIVELAND
   set self frame 0
endif
if self.state == BONK
   set self state BONKLAND
   set self frame 0
endif

|COLLIDE|
if self.state == PLATFORM
   set self touched 1
endif
set self calc self.state == BANANA
set self calc self.state == CHEESE or self.calc
if self.state == COIN or self.calc
   set self collected 1
endif

|checkAir|
if self.x_vel
   set self state AIRIAL
   set self frame 0
endif

|applyGrav|
set self x_vel self.x_vel - self.grav
if self.x_vel > self.limit
   set self x_vel self.limit
endif
if self.x_vel < self.neglimit
   set self x_vel self.neglimit
endif

|applyTraction|
if self.y_vel != 0
   if self.y_vel > 0
      set self y_vel self.traction + self.y_vel
   endif
   if self.y_vel < 0
      set self y_vel self.traction * -1 + self.y_vel
   endif
endif
if abs self.y_vel / 1 == 0
   set self y_vel 0
endif

|checkCrouch|
if inpLEFT
   set self state CROUCH
   set self frame 0
endif

|checkJump|
if A_DOWN in inpEVENTS
   set self state JUMPSQUAT
   set self frame 0
endif

