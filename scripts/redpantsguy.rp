REDPANTSGUY|128,256,32,64|True|
START:0 redpantsstand -16,0
IDLE:0 redpantsstand -16,0
RUNNING:0 redpantsrun0 -16,0
RUNNING:4 redpantsrun1 -16,0
RUNNING:8 redpantsrun2 -16,0
RUNNING:12 redpantsrun3 -16,0
AIRIAL:0 redpantsjump0 -16,0
AIRIAL:0 redpantsjump2 -16,0
AIRIAL:0 redpantsjump1 -16,0
SLIDE:0 redpantsslide1 -16,0
SLIDE:0 redpantsslide0 -16,0
LANDING:0 redpantsland -16,0
JUMPSQUAT:0 redpantsjumpsquat0 -16,0
JUMPSQUAT:3 redpantsjumpsquat1 -16,0
FLIPSQUAT:0 redpantsjumpsquat0 -16,0
FLIPSQUAT:3 redpantsjumpsquat1 -16,0
DIVESTART:0 redpantsdivestart -16,0
DIVE:0 redpantsdive -16,0
DIVELAND:0 redpantsdiveland -16,0
ROLLOUT:0 redpantsrollout -16,0
BONK:0 redpantsbonk -16,0
HITSTUN:0 redpantsbonk -16,0
BONKLAND:0 redpantsbonkland0 -16,0
BONKLAND:12 redpantsbonkland1 -16,0
WALLTOUCH:0 redpantswalljump0 -16,0
WALLJUMP:0 redpantswalljump1 -16,0
WALLJUMP:3 redpantswalljump2 -16,0
SLIDEFLIP:0 redpantsdiveflip0 -16,0
SLIDEFLIP:8 redpantsdiveflip1 -16,0
SLIDEFLIP:10 redpantsdiveflip2 -16,0
SLIDEFLIP:14 redpantsdiveflip3 -16,0
SLIDEFLIP:18 redpantsdiveflip4 -16,0
SLIDEFLIP:20 redpantsdiveflip5 -16,0
SLIDEFLIP:23 redpantsdiveflip6 -16,0
CROUCH:0 redpantscrouch -16,0
LONGJUMPSTART:0 redpantslongjumpstart -16,0
LONGJUMP:0 redpantslongjump1 -16,0
LONGJUMP:8 redpantslongjump2 -16,0
DEAD:0 redpantsdead -16,0
PUNCH:0 redpantspunch0 -32,0
PUNCH:4 redpantspunch1 -16,0
PUNCH:6 redpantspunch2 -32,0
PUNCH:8 redpantspunch3 -16,0
DANGLE:0 redpantsdangle -16,0
INVISIBLE:0 empty 0,0
DROWN:0 empty 0,0

|DROWN:45|
set self state IDLE
self self frame 0
exec respawn

|DANGLE:0|
if self.dangle_from
   set self x_vel 0
   set self y_vel 0
   set self y self.dangle_from.y + self.dangle_from.h - 32
   set self x self.dangle_from.x

   if A_DOWN in inpEVENTS
      set self state AIRIAL
      set self frame 0
      set self y_vel self.jumpstrength
   endif
endif

|START:0|
# welcome to red pants script :)
set self state IDLE
set self frame 0
set self speed 8
set self jumpstrength -17
set self hopstrength -12
set self divestrength 14
set self rolloutstrength -8
set self bonkstrength 2
set self walljumpstrength -12
set self walljumpoff 10
set self airdrift 0.7
set self traction 0.6 * -1
set self limit 20
set self airspeedcontrol 12
set self negairspeedcontrol self.airspeedcontrol * -1
set self negspeed self.speed * -1
set self neglimit self.limit * -1
set self grav 1
set self slideflipxmod 3
set self slideflipstrength -24
set self longjumpystrength -9
set self longjumpxstrength 12
hitboxes redpantsguy
hurtboxes redpantsguy
set self base redpantsguy
set self can_die 1
set self bloodnumber 0

set self rotation 0
set self door_key UP

set self width 32
set self height 64

|DEAD:0|
set self x_vel 0
set self y_vel 0
set self can_die 0
if self.frame % 4 == 0
   create blood0 self.name + blood + self.bloodnumber self.x + 16 self.y + 16
   set self bloodnumber self.bloodnumber + 1
endif

|DEAD:45|
set self state IDLE
self self frame 0
exec respawn

|respawn|
set self x self.respawnx
set self y self.respawny
view self.frame_name self.respawnworld
move self.name self.respawnworld
set self can_die 1

|IDLE:0|
exec checkAir
if self.x_vel != 0
   set self state SLIDE
endif
if self.y_vel == 0
   if not inpRIGHT and inpLEFT
      set self state RUNNING
      set self frame 0
      set self direction -1
   endif
   if not inpLEFT and inpRIGHT
      set self state RUNNING
      set self frame 0
      set self direction 1
   endif
endif
exec checkCrouch
exec checkJump
exec checkPunch
exec applyGrav

|IDLE:32|
exec IDLE:0
set self frame 0

|CROUCH:0|
exec checkAir
exec applyTraction
exec applyGrav
if not inpDOWN
   if self.x_vel == 0
      set self state IDLE
      set self frame 0
   endif
   if self.x_vel != 0
      set self state SLIDE
      set self frame 0
   endif
endif
# bufferable pog?
if self.x_vel != 0 and inpA
   set self state LONGJUMPSTART
   set self frame 0
endif
set self frame 0

|LONGJUMPSTART:0|
exec applyTraction
exec applyGrav

|LONGJUMPSTART:2|
exec LONGJUMPSTART:0
set self y_vel self.longjumpystrength
set self x_vel self.longjumpxstrength * self.direction
set self state LONGJUMP
set self frame 0

|LONGJUMP:0|
exec applyGrav
set self calc1 self.x_vel < self.negairspeedcontrol
if not self.calc1 and inpLEFT
   set self x_vel self.x_vel - self.airdrift
endif
set self calc1 self.x_vel > self.airspeedcontrol
if not self.calc1 and inpRIGHT
   set self x_vel self.x_vel + self.airdrift
endif

|RUNNING:0|
set self x_vel self.speed * self.direction
set self calc1 self.direction == 1 nor inpRIGHT
set self calc2 self.direction == -1 nor inpLEFT
if inpRIGHT == inpLEFT or self.calc1 or self.calc2
   set self state SLIDE
   set self frame 0
endif
exec checkCrouch
exec checkJump
exec checkPunch
exec checkAir
exec applyGrav

|RUNNING:16|
exec RUNNING:0
set self frame 0

|SLIDE:0|
if self.x_vel == 0
   set self state IDLE
   set self frame 0
endif
exec checkPunch
if self.x_vel != 0
   exec applyTraction
   set self absxvel abs self.x_vel
   if abs self.traction * 10 > self.absxvel
      img redpantsslide1
   endif
   exec checkCrouch
   exec checkJump
   if self.state == JUMPSQUAT
      if self.direction == 1 and inpLEFT
      	 set self state FLIPSQUAT
	 set self frame 0
      endif
      if self.direction == -1 and inpRIGHT
      	 set self state FLIPSQUAT
	 set self frame 0
      endif
   endif
   exec checkAir
   exec applyGrav
endif
if abs self.x_vel < self.speed
   set self calc1 self.x_vel > 0 and inpRIGHT
   set self calc1 not inpLEFT and self.calc1
   if self.calc1
      set self direction 1
   endif
   if not inpRIGHT and self.x_vel < 0 and inpLEFT
      set self direction -1
   endif
   if not inpRIGHT and self.x_vel < 0 and inpLEFT or self.calc1
      set self state RUNNING
      set self frame 0
   endif
endif

|AIRIAL:0|
set self calc1 self.y_vel > self.airspeedcontrol
set self calc2 self.airspeedcontrol * -1 > self.y_vel
if not self.calc1 nor self.calc2
   if self.x_vel - self.airdrift > self.negspeed and inpLEFT
	set self x_vel self.x_vel - self.airdrift
   endif
   if self.x_vel + self.airdrift < self.speed and inpRIGHT
      set self x_vel self.x_vel + self.airdrift
   endif
   if B_DOWN in inpEVENTS
      set self state DIVESTART
      set self frame 0
   endif
endif
if self.calc1
   img redpantsjump2
endif
if self.calc2
   img redpantsjump0
endif
exec applyGrav

|JUMPSQUAT:0|
exec applyTraction
exec applyGrav

|JUMPSQUAT:6|
set self state AIRIAL
if inpA
   set self y_vel self.jumpstrength
endif
if not inpA
   set self y_vel self.hopstrength
endif
set self frame 0

|LANDING:0|
exec checkAir
exec applyTraction
exec applyGrav
if self.x_vel > 0
   set self direction 1
endif
if self.x_vel < 0
   set self direction -1
endif

|LANDING:5|
if self.x_vel
   set self state SLIDE
   exec SLIDE:0
endif
if self.x_vel == 0
   set self state IDLE
   exec IDLE:0
endif

|DIVESTART:0|
set self y_vel 0
set self x_vel 0

|DIVESTART:5|
set self x_vel self.divestrength * self.direction
set self state DIVE

|DIVE:0|
exec applyGrav

|DIVELAND:0|
exec checkAir
exec applyTraction
exec applyGrav
# bufferable Pog?
if inpA
   set self state ROLLOUT
   set self frame 0
   set self y_vel self.rolloutstrength
endif
if self.x_vel == 0
   set self state IDLE
   set self frame 0
endif

|ROLLOUT:0|
exec applyGrav

|BONK:0|
exec applyGrav

|BONK:360|
set self state IDLE
set self frame 0

|BONKLAND:0|
exec checkAir
exec applyTraction
exec applyGrav

|BONKLAND:25|
set self state IDLE
set self frame 0

|WALLTOUCH:0|
exec AIRIAL:0
set self state AIRIAL
if not self.calc1 nor self.calc2
   img redpantswalljump0
   # bufferable pog?
   if inpA
      set self state WALLJUMP
      set self frame 0
   endif
endif

|WALLJUMP:0|
set self y_vel 0

|WALLJUMP:6|
set self state AIRIAL
set self frame 0
set self y_vel self.walljumpstrength
set self direction self.direction * -1
set self x_vel self.walljumpoff * self.direction

|FLIPSQUAT:0|
exec applyTraction
exec applyGrav

|FLIPSQUAT:6|
set self state SLIDEFLIP
set self direction self.direction * -1
set self x_vel self.slideflipxmod * self.direction
set self y_vel self.slideflipstrength
set self frame 0

|SLIDEFLIP:0|
exec applyGrav
if self.direction == -1
   if inpLEFT
      	set self x_vel self.x_vel - self.airdrift
	if self.x_vel < self.negairspeedcontrol
      	   set self x_vel self.negairspeedcontrol
   	endif
   endif
endif
if self.direction == 1
   if inpRIGHT
      set self x_vel self.x_vel + self.airdrift
      if self.x_vel > self.airspeedcontrol
      	 set self x_vel self.airspeedcontrol
      endif
   endif
endif

|PUNCH:0|
exec checkAir
exec applyTraction
exec applyGrav

|PUNCH:16|
set self state IDLE
set self frame 0

|XCOLLISION|
set self calc1 self.state == DIVE
set self calc1 self.state == DIVELAND or self.calc1
set self calc1 self.state == LONGJUMP or self.calc1
if self.state == ROLLOUT or self.calc1
   set self state BONK
   set self x_vel self.bonkstrength * self.direction * -1
   set self frame 0
endif
set self calc1 self.state == SLIDEFLIP
if self.state == AIRIAL or self.calc1
   set self state WALLTOUCH
   set self frame 0
   if self.x_vel > 0
      set self direction 1
   endif
   if self.x_vel < 0
      set self direction -1
   endif
endif

|YCOLLISION|
set self calc1 self.state == ROLLOUT
set self calc1 self.state == SLIDEFLIP or self.calc1
set self calc1 self.state == LONGJUMP or self.calc1
if self.state == AIRIAL or self.calc1
   set self state LANDING
   set self frame 0
endif
if self.state == DIVE
   set self state DIVELAND
   set self frame 0
endif
if self.state == BONK
   set self state BONKLAND
   set self frame 0
endif

|COLLIDE|
if self.state == PLATFORM
   set self touched 1
endif
set self calc self.state == BANANA
set self calc self.state == CHEESE or self.calc
set self calc self.state == COLLECTABLE or self.calc
if self.state == COIN or self.calc
   set self collected 1
endif

|HIT|
set self state HITSTUN
set self frame 0
set self direction related.direction * -1
if related.state == DIVE
   set self hit_x_vel 15 * related.direction
   set self hit_y_vel -10
endif
if related.state == PUNCH
   set self hit_x_vel 8 * related.direction
   set self hit_y_vel -8
endif

if self.thwacks == None
   set self thwacks 1
endif

if related.x <= self.x
   set self thw_x related.x + related.w
endif
if related.x > self.x
   set self thw_x related.x
endif
if related.y <= self.y
   set self thw_y self.y + related.y - self.y
endif
if related.y > self.y
   set self thw_y self.y + self.y - related.y
endif

create thwack0 self.name + thwack + self.thwacks self.thw_x self.thw_y
set self thwacks self.thwacks + 1


|HITSTUN:0|
set self x_vel 0
set self y_vel 0

|HITSTUN:4|
set self state BONK
set self frame 0
set self x_vel self.hit_x_vel
set self y_vel self.hit_y_vel
set self hit_x_vel 0
set self hit_y_vel 0

|checkAir|
set self dangle_from 0
if self.y_vel
   set self state AIRIAL
   set self frame 0
endif

|applyGrav|
set self y_vel self.y_vel + self.grav
if self.y_vel > self.limit
   set self y_vel self.limit
endif
if self.y_vel < self.neglimit
   set self y_vel self.neglimit
endif

if abs self.y_vel < 1
   if self.y_vel > 0
      if self.grav > 0
         set self y_vel 1
      endif
   endif
   if self.y_vel < 0
      if self.grav < 0
         set self y_vel -1
      endif
   endif
endif

|applyTraction|
if self.x_vel != 0
   if self.x_vel > 0
      set self x_vel self.traction + self.x_vel
   endif
   if self.x_vel < 0
      set self x_vel self.traction * -1 + self.x_vel
   endif
endif
if abs self.x_vel / 1 == 0
   set self x_vel 0
endif

|checkCrouch|
if inpDOWN
   set self state CROUCH
   set self frame 0
endif

|checkJump|
if A_DOWN in inpEVENTS
   set self state JUMPSQUAT
   set self frame 0
endif

|checkPunch|
if B_DOWN in inpEVENTS
   set self state PUNCH
   set self frame 0
endif

